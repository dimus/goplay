package grammar

type Engine Peg {
}

SciName <- _? SciName1 (.*) !.

SciName1 <- SciName2

SciName2 <- Name

HybridFormula <- Name (_ (HybridFormula1 / HybridFormula2))

HybridFormula1 <- HybridChar _? SpeciesEpithet (_ InfraspGroup)?

HybridFormula2 <- HybridChar (_ Name)?

NamedHybrid <- HybridChar _? Name

Name <- NameSpecies / NameUninomial

NameUninomial <- UninomialCombo / Uninomial

NameApprox <- GenusWord _ Approximation (_ SpeciesEpithet)?

NameComp <- GenusWord _ Comparison (_ SpeciesEpithet)?

NameSpecies <- GenusWord (_? (SubGenus / SubGenusOrSuperspecies))?
               _ SpeciesEpithet (_ InfraspGroup)?

GenusWord <- (AbbrGenus / UninomialWord) !(_ AuthorWord)

InfraspGroup <- InfraspEpithet (_ InfraspEpithet)?  (_ InfraspEpithet)?

InfraspEpithet <- (Rank _?)? !(AuthorEx) Word  (_ Authorship)?

SpeciesEpithet <- !(AuthorEx) Word (_? Authorship)? ','? &(SpaceCharEOI / '(')

Comparison <- ('cf' '.'?)

Rank <- RankForma / RankVar / RankSsp / RankOther / RankOtherUncommon

RankOtherUncommon <- ('*' / 'nat' / 'f.sp' / 'mut.') &(SpaceCharEOI)

RankOther <- ('morph.' / 'nothosubsp.' / 'convar.' / 'pseudovar.' / 'sect.' /
  'ser.' / 'subvar.' / 'subf.' / 'race' / 'α' / 'ββ' / 'β' / 'γ' / 'δ' / 'ε' /
  'φ' / 'θ' / 'μ' / 'a.' / 'b.' / 'c.' / 'd.' / 'e.' / 'g.' / 'k.' / 'pv.' /
  'pathovar.' / ('ab.' (_? 'n.')?) / 'st.') &(SpaceCharEOI)

RankVar <- 'variety' / '[var.]' / 'nvar.' / ('var' (&(SpaceCharEOI) / '.'))

RankForma <- ('forma' / 'fma' / 'form' / 'fo' / 'f') (&(SpaceCharEOI) / '.')

RankSsp <- ('ssp' / 'subsp') (&(SpaceCharEOI) / '.')

SubGenusOrSuperspecies <- '(' _? Word _? ')'

SubGenus <- '(' _? UninomialWord _? ')'

UninomialCombo <- UninomialCombo1 / UninomialCombo2

UninomialCombo1 <- UninomialWord _? SubGenus _? Authorship.?

UninomialCombo2 <- Uninomial _? RankUninomial _? Uninomial

RankUninomial <- ('sect' / 'subsect' / 'trib' / 'subtrib' / 'subser' / 'ser' /
  'subgen' / 'fam' / 'subfam' / 'supertrib') '.'?

Uninomial <- UninomialWord (_ Authorship)?

UninomialWord <- CapWord / TwoLetterGenus

AbbrGenus <- UpperChar LowerChar* '.'

CapWord <- CapWord2 / CapWord1

CapWord1 <- NameUpperChar NameLowerChar NameLowerChar+ '?'?

CapWord2 <- CapWord1 dash (CapWord1 / Word1)

TwoLetterGenus <- ('Ca' / 'Ea' / 'Ge' / 'Ia' / 'Io' / 'Ix' / 'Lo' / 'Oa' /
  'Ra' / 'Ty' / 'Ua' / 'Aa' / 'Ja' / 'Zu' / 'La' / 'Qu' / 'As' / 'Ba')

Word <- !(AuthorPrefix / RankUninomial / Approximation / Word4)
      (Word3 / Word2StartDigit / Word2 / Word1) &(SpaceCharEOI / "()")

Word1 <- (lASCII dash)? NameLowerChar NameLowerChar+

Word2StartDigit <- [123456789] nums? ('.' / dash)? NameLowerChar NameLowerChar
  NameLowerChar NameLowerChar+

Word2 <- NameLowerChar+ dash? NameLowerChar

Word3 <- NameLowerChar NameLowerChar* apostr Word1

Word4 <- NameLowerChar+ '.' NameLowerChar

HybridChar <- '×'

ApproxName <- Uninomial _ (ApproxName1 / ApproxName2)

ApproxName1 <- Approximation ApproxNameIgnored

ApproxName2 <- Word _ Approximation ApproxNameIgnored

ApproxNameIgnored <- .*

Approximation <- ('sp.' _? 'nr.' / 'sp.' _? 'aff.' / 'monst.' /
  '?' / (('spp' / 'nr' / 'sp' / 'aff' / 'species') (&(SpaceCharEOI) / '.')))

Authorship <- (Authorship1 / Authorship2) &(SpaceCharEOI / [\\(,:])

Authorship1 <- Authorship2 _? AuthorsGroup

Authorship2 <- (AuthorsGroup / BasionymAuthorship /
  BasionymAuthorshipYearMisformed)

BasionymAuthorshipYearMisformed <- '(' _? AuthorsGroup _? ')' (_? ',')? _? Year

BasionymAuthorship <- BasionymAuthorship1 / BasionymAuthorship2

BasionymAuthorship1 <- '(' _? AuthorsGroup _? ')'

BasionymAuthorship2 <- '(' _? '(' _? AuthorsGroup _? ')' _? ')'

AuthorsGroup <- AuthorsTeam (_? AuthorEmend? AuthorEx? AuthorsTeam)?

AuthorsTeam <- Author (AuthorSep Author)* (','? _? Year)?

AuthorSep <- AuthorSep1 / AuthorSep2

AuthorSep1 <- _? (',' _)? ( '&' / 'et' / 'apud') _?

AuthorSep2 <- _? ',' _?

AuthorEx <- ('ex' '.'? / 'in') _

AuthorEmend <- 'emend' '.'? _

Author <- (Author1 / Author2 / UnknownAuthor)

Author1 <- Author2 _ Filius

Author2 <- AuthorWord (_? AuthorWord)*

UnknownAuthor <- '?' / (('auct' / 'anon') (&(SpaceCharEOI) / '.'))

AuthorWord <- AuthorWord1 /AuthorWord2 / AuthorWord3 / AuthorPrefix

AuthorWord1 <- 'arg.' / 'et al.{?}' / ('et' / '&') ' al' '.'?

AuthorWord2 <- AuthorWord3 dash AuthorWordSoft

AuthorWord3 <- AuthorPrefixGlued? AuthorUpperChar (AuthorUpperChar /
  AuthorLowerChar)* '.'?

AuthorWordSoft <- ((AuthorUpperChar (AuthorUpperChar+ / AuthorLowerChar+)) /
  AuthorLowerChar+) '.'?

Filius <- 'f.' / 'fil.' / 'filius'

AuthorPrefixGlued <- 'd\'' / 'O\''

AuthorPrefix <- AuthorPrefix1 / AuthorPrefix2

AuthorPrefix2 <- ('v.' (_? 'd.')?) / '\'t'

AuthorPrefix1 <- ('ab' / 'af' / 'bis' / 'da' / 'der' / 'des' / 'den' / 'del' /
  'della' / 'dela' / 'de' / 'di' / 'du' / 'el' / 'la' / 'le' / 'ter' / 'van' /
  'd\'' / 'in\'t' / 'zur' / ('von' (_ 'd.')?) / ('v' (_'d')?)) &_

AuthorUpperChar <- hASCII /
  [ÀÁÂÃÄÅÆÇÈÉÊËÌÍÎÏÐÑÒÓÔÕÖØÙÚÛÜÝĆČĎİĶĹĺĽľŁłŅŌŐŒŘŚŜŞŠŸŹŻŽƒǾȘȚ]


AuthorLowerChar <- lASCII /
  [àáâãäåæçèéêëìíîïðñòóóôõöøùúûüýÿāăąćĉčďđ'ēĕėęěğīĭİıĺľłńņňŏőœŕřśşšţťũūŭůűźżžſǎǔǧșțȳß]

Year <- YearRange / YearApprox / YearWithParens / YearWithPage / YearWithDot /
  YearWithChar / YearNum

YearRange <- YearNum dash (nums+ [abcdefghijklmnopqrstuvwxyz?]*)

YearWithDot <-  YearNum '.'

YearApprox  <- '[' _? YearNum _? ']'

YearWithPage <- (YearWithChar / YearNum)  _? ":" _? nums+

YearWithParens <- '(' (YearWithChar / YearNum) ')'

YearWithChar <- YearNum lASCII

YearNum <- [12] [0789] nums (nums / '?') '?'*

NameUpperChar <- UpperChar / UpperCharExtended

UpperCharExtended <- [ÆŒÖ]

UpperChar <- hASCII

NameLowerChar <- LowerChar / LowerCharExtended / MiscodedChar

MiscodedChar <- '�'

LowerCharExtended <- [æœſàâåãäáçčéèíìïňññóòôøõöúùüŕřŗššşž]

LowerChar <- [a-zë]

SpaceCharEOI <- _ / !.

WordBorderChar <- _ / [;.,;()]

nums <- [0-9]

lASCII <- [a-z]

hASCII <- [A-Z]

apostr <- '\''

dash <- '-'

_ <- ' '+
